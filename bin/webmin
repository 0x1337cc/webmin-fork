#!/usr/bin/env perl
# Webmin CLI - Allows performing a variety of common Webmin-related
# functions on the command line.
use strict;
use warnings;
BEGIN { $Pod::Usage::Formatter = 'Pod::Text::Color'; }
use 5.010; # Version in CentOS 6

use Getopt::Long qw(:config permute pass_through);
use Pod::Usage;
use Term::ANSIColor qw(:constants);
use File::Spec;

sub main {
    my ( $argv ) = @_;
    my ( %opt, $subcmd );
    GetOptions(
        'help|h' => \$opt{'help'},
        'config|c=s' => \$opt{'config'},
        'list-commands|l' => \$opt{'list'},
        '<>' => sub {
            # Handled unrecognized options, inc. subcommands.
            my($arg) = @_;
            if ($arg =~ m{^-}) {
                say "Usage error: Unknown option $arg.";
                pod2usage(0);
            } else {
                # It must be a subcommand.
                $subcmd = $arg;
                die "!FINISH";
            }
        }
    );
    pod2usage(0) if ( $opt{'help'} );

    $opt{'config'} ||= "/etc/webmin";

    my @remain = @ARGV;
    if ($subcmd) {
        run_command( \%opt, $subcmd, \@remain );
    }
    
    return 0;
}
exit main( \@ARGV ) if !caller(0);

# run_command - Run a subcommand 
# $optref is a reference to an options object passed down from global options
# like --help or a --config path.
sub run_command {
    my ( $optref, $subcmd, $remainref ) = @_;

    # Figure out the Webmin root directory
    my $root = root($optref->{'config'});

    my ($command, $module_name);
    # Check for a root-level command (in "$root/bin")
    my $command_path = File::Spec->catfile($root, 'bin', $subcmd);
    if ( -x $command_path) {
        $command = $command_path;
    } else {
        # Try to extract a module name from the command
        # Get list of directories
        opendir (my $DIR, $root);
        my @module_dirs = grep { -d "$root/$_" } readdir($DIR);
        # See if any of them are a substring of $subcmd
        for my $dir (@module_dirs) {
            if (index($subcmd, $dir) == 0) {
                $module_name = $dir;
                my $barecmd = substr($subcmd, -(length($subcmd)-length($module_name)-1));
                $command = File::Spec->catfile($root, $dir, 'bin', $barecmd);
                # Could be .pl or no extension
                if ( -x $command ) {
                    last;
                } elsif ( -x $command . ".pl" ) {
                    $command = $command . ".pl";
                    last;
                } else {
                    die "$command doesn't exist or is not executable"
                }
            }
        }
    }

    # Merge the options
    # Only handling config, right now...
    # XXX Should we do this with libraries instead of commands?
    # Would need less boilerplate if they weren't standalone commands.
    my @allopts = ("--config $optref->{'config'}", @$remainref);
    # Run that binch
    system($command, @allopts);
}

sub root {
    my ($config) = @_;
    if ($config) {
        $ENV{'WEBMIN_CONFIG'} = $config;
    }
    $ENV{'WEBMIN_CONFIG'} ||= "/etc/webmin";
    open(my $CONF, "<", "$ENV{'WEBMIN_CONFIG'}/miniserv.conf") || die RED,
        "Failed to open miniserv.conf", RESET;
    my $root;
    while (<$CONF>) {
        if (/^root=(.*)/) {
            $root = $1;
        }
    }
    close($CONF);
    # Does the Webmin root exist?
    if ( $root ) {
        die "$root is not a directory. Is --config correct?" unless (-d $root);
    } else {
        # Try to guess where Webmin lives, since config file didn't know.
        die "Unable to determin Webmin installation directory from $ENV{'WEBMIN_CONFIG'}";
    }

    return $root;
}

1;

=pod

=head1 NAME

webmin

=head1 SYNOPSIS

webmin [options] subcommand [subcommand options]

=head1 OPTIONS

=over

=item --help, -h

Print this usage summary and exit. Subcommands may also have a usage summary.

=item --config, -c

Specify the full path to the Webmin miniserv.conf file. Defaults to
C</etc/webmin/miniserv.conf>

=item --list-commands, -l

List available subcommands.

=head1 EXIT CODES

0 on success

non-0 on error

=head1 LICENSE AND COPYRIGHT

Copyright 2018 Jamie Cameron <jcameron@webmin.com>, Joe Cooper
<joe@virtualmin.com>.

